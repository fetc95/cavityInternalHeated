set term pdfcairo color size 16cm,6.5cm font 'Times-New-Roman,15'
set out 'two-layer-charge.pdf'
source="two-layer.dat"
set multiplot
set lmargin 7
set rmargin 1
set xlabel 'Time, h' offset 0,.8
set xtics offset 0,0.4
set ylabel '{/Symbol-Italic h}_{/Times-New-Roman-Italic s}, {/Times-New-Roman-Italic E_s}' offset graph 0.03,graph 0
set xrange [:4.1]
set yrange [0:1]
set key rev r c
array dd[4] = [5,1,2,4]
array cc[4] = [3,1,6,7]
array seta[4] = ["[GR27-GR41-charge, energy]","[GR41-GR27-charge, energy]","[GR27-GR41-discharge, energy]","[GR41-GR27-discharge, energy]"]
set size 0.48,1
plot for [i=1:2] source ind seta[i] u (column(1)/3600):(column(2)/4.64142) w l lw 3 dt dd[i] lc cc[i] ti "{/Times-New-Roman-Italic ".i."}",\
for [i=1:2] source ind seta[i] u (column(1)/3600):(column(4)/30865.76) w l lw 3 dt dd[i+2] lc cc[i+2] ti "{/Times-New-Roman-Italic ".(i+2)."}"
set origin 0.5,0
set ylabel '{/Symbol-Italic h}_{/Times-New-Roman-Italic r}, {/Times-New-Roman-Italic E_r}'
plot for [i=1:2] source ind seta[i+2] u (column(1)/3600):(column(2)/5.1882) w l lw 3 dt dd[i] lc cc[i] ti "{/Times-New-Roman-Italic ".(i+4)."}",\
for [i=1:2] source ind seta[i+2] u (column(1)/3600):(column(4)/30865.76) w l lw 3 dt dd[i+2] lc cc[i+2] ti "{/Times-New-Roman-Italic ".(i+6)."}"
unset multiplot
set out

#------------------------------------------------------------------------------
